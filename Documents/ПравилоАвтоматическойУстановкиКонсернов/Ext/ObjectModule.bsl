
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПервыйЗаказ.Пустая() И ПервыйЗаказ = ПоследнийЗаказ Тогда
		ТекстСообщения = "Последний заказ не может быть равен первому";
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ПоследнийЗаказ",, Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если Включено Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	ДвижениеЗаказовОбороты.Заказ КАК Заказ
			|ИЗ
			|	РегистрНакопления.ДвижениеЗаказов.Обороты(
			|			,
			|			,
			|			Период,
			|			ВЫБОР
			|				КОГДА &ПервыйЗаказ = ЗНАЧЕНИЕ(Справочник.Заказы.ПустаяСсылка)
			|					ТОГДА ИСТИНА
			|				ИНАЧЕ Заказ = &ПервыйЗаказ
			|						И ТочкаРегистрации = &ТочкаРегистрацииMES
			|			КОНЕЦ) КАК ДвижениеЗаказовОбороты
			|ГДЕ
			|	ДвижениеЗаказовОбороты.КоличествоПриход > 0
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ ПЕРВЫЕ 1
			|	ДвижениеЗаказовОбороты.Заказ КАК Заказ
			|ИЗ
			|	РегистрНакопления.ДвижениеЗаказов.Обороты(
			|			,
			|			,
			|			Период,
			|			ВЫБОР
			|				КОГДА &ПоследнийЗаказ = ЗНАЧЕНИЕ(Справочник.Заказы.ПустаяСсылка)
			|					ТОГДА ИСТИНА
			|				ИНАЧЕ Заказ = &ПоследнийЗаказ
			|						И ТочкаРегистрации = &ТочкаРегистрацииMES
			|			КОНЕЦ) КАК ДвижениеЗаказовОбороты
			|ГДЕ
			|	ДвижениеЗаказовОбороты.КоличествоПриход > 0";
		
		Запрос.УстановитьПараметр("ПервыйЗаказ", ПервыйЗаказ);
		Запрос.УстановитьПараметр("ПоследнийЗаказ", ПоследнийЗаказ);
		Запрос.УстановитьПараметр("ТочкаРегистрацииMES", ТочкаРегистрацииMES);
		РезультатыЗапроса = Запрос.ВыполнитьПакет();
		
		Действует = Не РезультатыЗапроса[0].Пустой() И Не РезультатыЗапроса[1].Пустой();
		
		Движения.ПравилаАвтоматическойУстановкиКонсернов.Записывать = Истина;
		Движение = Движения.ПравилаАвтоматическойУстановкиКонсернов.Добавить();
		Движение.Период 				= Дата;
		Движение.Правило 				= Ссылка;
		Движение.ТочкаРегистрацииMES 	= ТочкаРегистрацииMES;
		Движение.ПервыйЗаказ 			= ПервыйЗаказ;
		Движение.ПоследнийЗаказ 		= ПоследнийЗаказ;
		Движение.Действует 				= Действует;
		
	КонецЕсли;

КонецПроцедуры

