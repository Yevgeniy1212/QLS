
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	

	Движения.ДвижениеЗаказов.Записывать = Истина;
	
	Движение = Движения.ДвижениеЗаказов.Добавить();
	
	Если ЗначениеЗаполнено(ПредшествующаяТочкаРегистрации) Тогда
		Движение.Период				 = Дата;
		Движение.ВидДвижения		 = ВидДвиженияНакопления.Расход;
		Движение.Заказ		 		 = Заказ;
		Движение.ТочкаРегистрации	 = ПредшествующаяТочкаРегистрации;
		Движение.Количество			 = 1;
		Движение = Движения.ДвижениеЗаказов.Добавить();
	КонецЕсли;

	Если ЗначениеЗаполнено(ТочкаРегистрации) Тогда
		Движение.Период							 = Дата;	
		Движение.ВидДвижения					 = ВидДвиженияНакопления.Приход;
		Движение.Заказ					 		 = Заказ;
		Движение.ТочкаРегистрации				 = ТочкаРегистрации;
		Движение.ПредшествующаяТочкаРегистрации	 = ПредшествующаяТочкаРегистрации;
		Движение.Количество						 = 1;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПравилаАвтоматическойУстановкиКонсерновСрезПоследних.Правило КАК Правило,
		|	ПравилаАвтоматическойУстановкиКонсерновСрезПоследних.ТочкаРегистрацииMES КАК ТочкаРегистрацииMES,
		|	ПравилаАвтоматическойУстановкиКонсерновСрезПоследних.ПервыйЗаказ КАК ПервыйЗаказ,
		|	ПравилаАвтоматическойУстановкиКонсерновСрезПоследних.ПоследнийЗаказ КАК ПоследнийЗаказ,
		|	ПравилаАвтоматическойУстановкиКонсерновСрезПоследних.Действует КАК Действует
		|ИЗ
		|	РегистрСведений.ПравилаАвтоматическойУстановкиКонсернов.СрезПоследних(
		|			,
		|			ТочкаРегистрацииMES = &ТочкаРегистрации
		|				И (ПервыйЗаказ = &Заказ
		|					ИЛИ ПоследнийЗаказ = &Заказ)) КАК ПравилаАвтоматическойУстановкиКонсерновСрезПоследних";
	
	Запрос.УстановитьПараметр("ТочкаРегистрации", ТочкаРегистрации);
	Запрос.УстановитьПараметр("Заказ", Заказ);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		Действует = Заказ = Выборка.ПервыйЗаказ;
		
		Движения.ПравилаАвтоматическойУстановкиКонсернов.Записывать = Истина;
		Движение = Движения.ПравилаАвтоматическойУстановкиКонсернов.Добавить();
		Движение.Период 				= Дата;
		Движение.ТочкаРегистрацииMES 	= Выборка.ТочкаРегистрацииMES;
		Движение.ПервыйЗаказ 			= Выборка.ПервыйЗаказ;
		Движение.ПоследнийЗаказ 		= Выборка.ПоследнийЗаказ;
		Движение.Действует 				= Действует;
		
	КонецЕсли;
		
	Движения.Записать();
	
КонецПроцедуры
