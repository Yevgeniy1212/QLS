#Область СлужебныеПроцедурыИФункции

// put-xdto-orders-reg-registrations
Функция ЗаписатьРегистрациюЗаказов(Запрос)
	
	Попытка
		Ответ = ЗаписатьРегистрациюЗаказовОсновнаяЛогика(Запрос);
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
		ЗаписьЖурналаРегистрации(СобытиеЖурнала(НСтр("ru = 'Ошибка выполнения'", ОбщегоНазначения.КодОсновногоЯзыка())), 
			УровеньЖурналаРегистрации.Ошибка, Метаданные.HTTPСервисы.OrdersRegistrations,"OrdersRegistrations.PUT", ОписаниеОшибки);
		
		Ответ = ОтветОшибка("Server error", 500);
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции

Функция ЗаписатьРегистрациюЗаказовОсновнаяЛогика(Запрос)
		
	СтрокаXML = Запрос.ПолучитьТелоКакСтроку();

	ОбщегоНазначенияQLS.ЗаписатьВЖурналРегистрацииПараметрыЗапроса(Запрос, Метаданные.HTTPСервисы.OrdersRegistrations, 
		"OrdersRegistrations.PUT", СобытиеЖурнала(НСтр("ru = 'HTTP запрос'", ОбщегоНазначения.КодОсновногоЯзыка())), СтрокаXML);
		
	Версия = Запрос.ПараметрыURL["ver"];
	ИдентификаторСсылки = Запрос.ПараметрыURL["idVIN"];
	
	ПараметрыЗагрузки = Новый Структура();
	ПараметрыЗагрузки.Вставить("ИдентификаторСсылки", ИдентификаторСсылки);		
	ПараметрыЗагрузки.Вставить("Версия", Версия);		
	ПараметрыЗагрузки.Вставить("ТипОбъекта", "ДвиженияЗаказов");

	ОбменРегистрациейЗаказов.ЗарегистрироватьДвиженияЗаказов(ПараметрыЗагрузки, СтрокаXML);
		
	Возврат Ответ(200);
КонецФункции

Функция Ответ(КодОтвета = 200)
	
	Ответ = Новый HTTPСервисОтвет(КодОтвета);
	Возврат Ответ;
	
КонецФункции

// put-xdto-orders-reg-registration-point
Функция ЗаписатьТочкуРегистрации(Запрос)
	
	Попытка
		Ответ = ЗаписатьТочкуРегистрацииОсновнаяЛогика(Запрос);
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка загрузки сообщения: %1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));

		ЗаписьЖурналаРегистрации(СобытиеЖурнала(НСтр("ru = 'Ошибка выполнения'", ОбщегоНазначения.КодОсновногоЯзыка())), 
			УровеньЖурналаРегистрации.Ошибка, Метаданные.HTTPСервисы.OrdersRegistrations,"RegistrationPoint.PUT", ОписаниеОшибки);
		Ответ = ОтветОшибка("Server error", 500);
	КонецПопытки;
	
	Возврат Ответ;
КонецФункции

Функция ЗаписатьТочкуРегистрацииОсновнаяЛогика(Запрос)

	СтрокаXML = Запрос.ПолучитьТелоКакСтроку();

	ОбщегоНазначенияQLS.ЗаписатьВЖурналРегистрацииПараметрыЗапроса(Запрос, Метаданные.HTTPСервисы.OrdersRegistrations, 
		"RegistrationPoint.PUT", СобытиеЖурнала(НСтр("ru = 'HTTP запрос'", ОбщегоНазначения.КодОсновногоЯзыка())), СтрокаXML);

	Версия = Запрос.ПараметрыURL["ver"];
	ИдентификаторСсылки = Запрос.ПараметрыURL["id"];
	
	ЭтоГруппа = Запрос.ПараметрыЗапроса.Получить("ЭтоГруппа") <> Неопределено;

	ПараметрыЗагрузки = Новый Структура();
	ПараметрыЗагрузки.Вставить("ИдентификаторСсылки", ИдентификаторСсылки);		
	ПараметрыЗагрузки.Вставить("Версия", Версия);		
	ПараметрыЗагрузки.Вставить("ТипСправочника", "ТочкиРегистрации");
	ПараметрыЗагрузки.Вставить("ЭтоГруппа", ЭтоГруппа);

	ОбменСправочниками.ЗагрузитьСообщениеОбмена(ПараметрыЗагрузки, СтрокаXML);

	Возврат Ответ(204);

КонецФункции

Функция ОтветОшибка(ТекстОтвета, КодОтвета = 500)
	
	Ответ = Новый HTTPСервисОтвет(КодОтвета);
	Ответ.УстановитьТелоИзСтроки(ТекстОтвета);
	Возврат Ответ;
	
КонецФункции

Функция СобытиеЖурнала(ПодчиненноеСобытие = "")
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Регистрация заказов%1'", ОбщегоНазначения.КодОсновногоЯзыка()), 
				?(ПустаяСтрока(ПодчиненноеСобытие),"",". ") + ПодчиненноеСобытие);

КонецФункции

#КонецОбласти
