
#Область СлужебныеПроцедурыИФункции

Функция ПолучитьСписокДефектовПоСпискуИменДефектов(ТекстовыйСписокДефектов, ТекстОшибки) Экспорт

	ТекстовыйСписокДефектов = СокрЛП(ТекстовыйСписокДефектов);
	
	СписокДефектов = Новый СписокЗначений;
	Если ПустаяСтрока(ТекстовыйСписокДефектов) Тогда
		Возврат СписокДефектов;
	КонецЕсли; 
    МассивСтрокДефектов = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ТекстовыйСписокДефектов, Символы.ПС);
	
	ИсходнаяТаблица = Новый ТаблицаЗначений;
	ИсходнаяТаблица.Колонки.Добавить("КлючПоиска", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(20)));
	ИсходнаяТаблица.Колонки.Добавить("НомерИсходнойСтроки", Новый ОписаниеТипов("Число"));
	
	Счетчик = 0;
	Для каждого СтроковыйДефект Из МассивСтрокДефектов Цикл
		Счетчик = Счетчик + 1;
		СтрокаТаблицы = ИсходнаяТаблица.Добавить();
		Если Не ПустаяСтрока(СтроковыйДефект) Тогда
			СтрокаТаблицы.КлючПоиска = СтроковыйДефект;
			СтрокаТаблицы.НомерИсходнойСтроки = Счетчик;
		КонецЕсли;
	КонецЦикла;

	МассивТекстов = Новый Массив;
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИсходнаяТаблица.КлючПоиска КАК КлючПоиска,
		|	ИсходнаяТаблица.НомерИсходнойСтроки КАК НомерИсходнойСтроки
		|ПОМЕСТИТЬ ИсходнаяТаблица
		|ИЗ
		|	&ИсходнаяТаблица КАК ИсходнаяТаблица
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИсходнаяТаблица.КлючПоиска КАК КлючПоиска,
		|	ИсходнаяТаблица.НомерИсходнойСтроки КАК НомерИсходнойСтроки,
		|	ЕСТЬNULL(Дефекты.Ссылка, ЗНАЧЕНИЕ(Справочник.Дефекты.ПустаяСсылка)) КАК Дефект
		|ИЗ
		|	ИсходнаяТаблица КАК ИсходнаяТаблица
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Дефекты КАК Дефекты
		|		ПО ИсходнаяТаблица.КлючПоиска = Дефекты.Наименование
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерИсходнойСтроки";

	Запрос.УстановитьПараметр("ИсходнаяТаблица",ИсходнаяТаблица);
	Результат = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = Результат.Выбрать();

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Дефект = ВыборкаДетальныеЗаписи.Дефект;
		Если НЕ ЗначениеЗаполнено(Дефект) Тогда
			ШаблонСообщения = НСтр("ru = 'строка №%1: дефект по имени ""%2"" не найден.'");
			ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, ВыборкаДетальныеЗаписи.НомерИсходнойСтроки, СокрЛП(ВыборкаДетальныеЗаписи.КлючПоиска));
			МассивТекстов.Добавить(ОписаниеОшибки);
		Иначе
			Если СписокДефектов.НайтиПоЗначению(Дефект) = Неопределено Тогда
				СписокДефектов.Добавить(Дефект);
			КонецЕсли;
		КонецЕсли; 
	КонецЦикла;
	
	ТекстОшибки = СтрСоединить(МассивТекстов, Символы.ПС);

	Возврат СписокДефектов;
КонецФункции 

#КонецОбласти
