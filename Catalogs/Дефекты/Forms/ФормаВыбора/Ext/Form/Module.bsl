#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтборПоПеречнюДефектов(Команда)
	перем времТекстовыйПереченьДефектов;
	СписокДефектов = Новый СписокЗначений;
	
	Если Элементы.СписокОтборПоПеречнюДефектов.Пометка Тогда
		ИспользованиеОтбора = Ложь;
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
									"Ссылка",
									СписокДефектов,
									ВидСравненияКомпоновкиДанных.ВСписке,
									,
									ИспользованиеОтбора);
									
		Элементы.СписокОтборПоПеречнюДефектов.Пометка = ИспользованиеОтбора;
			
	Иначе
		времТекстовыйПереченьДефектов = ТекстовыйПереченьДефектов;
		
		Подсказка = НСтр("ru = 'Введите наименования дефектов (каждый с новой строки)'");
		Оповещение = Новый ОписаниеОповещения("ОтборПоПеречнюДефектовПослеВводаСтроки", ЭтотОбъект);
		ПоказатьВводСтроки(Оповещение, времТекстовыйПереченьДефектов, Подсказка, , Истина);
		
	КонецЕсли; 
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОтборПоПеречнюДефектовПослеВводаСтроки(Строка, Параметры) Экспорт

    Если Строка = Неопределено Тогда
		Возврат;
    КонецЕсли;
    
    СписокДефектов = Новый СписокЗначений;
    
	Если Не ЗначениеЗаполнено(Строка) Тогда
		ИспользованиеОтбора = Ложь;
	Иначе
		ТекстОшибки = "";
		СписокДефектов = ПолучитьСписокДефектовПоСпискуИменДефектов(Строка, ТекстОшибки);
		Если Не ПустаяСтрока(ТекстОшибки) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
			ИспользованиеОтбора = Ложь;
		Иначе
			ИспользованиеОтбора = Истина;
			ТекстовыйПереченьДефектов = Строка;
		КонецЕсли; 
	КонецЕсли;
	 
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
								"Ссылка",
								СписокДефектов,
								ВидСравненияКомпоновкиДанных.ВСписке,
								,
								ИспользованиеОтбора);
								
	Элементы.СписокОтборПоПеречнюДефектов.Пометка = ИспользованиеОтбора; 
    
КонецПроцедуры

&НаСервере
Функция ПолучитьСписокДефектовПоСпискуИменДефектов(ТекстовыйСписокДефектов, ТекстОшибки)
	Возврат Справочники.Дефекты.ПолучитьСписокДефектовПоСпискуИменДефектов(ТекстовыйСписокДефектов, ТекстОшибки);
КонецФункции 

#КонецОбласти
