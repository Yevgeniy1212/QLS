#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтборПоПеречнюИнспектируемыхЭлементов(Команда)
	перем времТекстовыйПереченьДефектов;
	СписокИЭ = Новый СписокЗначений;
	
	Если Элементы.СписокОтборПоПеречнюИнспектируемыхЭлементов.Пометка Тогда
		ИспользованиеОтбора = Ложь;
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
									"Ссылка",
									СписокИЭ,
									ВидСравненияКомпоновкиДанных.ВСписке,
									,
									ИспользованиеОтбора);
									
		Элементы.СписокОтборПоПеречнюИнспектируемыхЭлементов.Пометка = ИспользованиеОтбора;
			
	Иначе
		времТекстовыйПереченьДефектов = ТекстовыйПереченьИнспектируемыхЭлементов;
		
		Подсказка = НСтр("ru = 'Введите наименования инспектируемых элементов (каждый с новой строки)'");
		Оповещение = Новый ОписаниеОповещения("ОтборПоПеречнюИЭПослеВводаСтроки", ЭтотОбъект);
		ПоказатьВводСтроки(Оповещение, времТекстовыйПереченьДефектов, Подсказка, , Истина);
		
	КонецЕсли; 
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОтборПоПеречнюИЭПослеВводаСтроки(Строка, Параметры) Экспорт

    Если Строка = Неопределено Тогда
		Возврат;
    КонецЕсли;
    
    СписокИЭ = Новый СписокЗначений;
    
	Если Не ЗначениеЗаполнено(Строка) Тогда
		ИспользованиеОтбора = Ложь;
	Иначе
		ТекстОшибки = "";
		СписокИЭ = ПолучитьСписокИЭПоСпискуИменДефектов(Строка, ТекстОшибки);
		Если Не ПустаяСтрока(ТекстОшибки) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
			ИспользованиеОтбора = Ложь;
		Иначе
			ИспользованиеОтбора = Истина;
			ТекстовыйПереченьИнспектируемыхЭлементов = Строка;
		КонецЕсли; 
	КонецЕсли;
	 
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
								"Ссылка",
								СписокИЭ,
								ВидСравненияКомпоновкиДанных.ВСписке,
								,
								ИспользованиеОтбора);
								
	Элементы.СписокОтборПоПеречнюИнспектируемыхЭлементов.Пометка = ИспользованиеОтбора; 
    
КонецПроцедуры

&НаСервере
Функция ПолучитьСписокИЭПоСпискуИменДефектов(ТекстовыйСписок, ТекстОшибки)
	Возврат ОбщегоНазначенияQLS.ПолучитьСписокЭлементовИзСправочникаПоСпискуИмен(ТекстовыйСписок, "ИнспектируемыеЭлементы", ТекстОшибки, 150); 
КонецФункции 

#КонецОбласти
