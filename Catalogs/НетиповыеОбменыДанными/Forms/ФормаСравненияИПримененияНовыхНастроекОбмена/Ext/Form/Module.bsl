&НаСервереБезКонтекста
Функция ПрименитьНовыеНастройки_Сервер(Док)

	ДокОбъект = Док.ПолучитьОбъект();
	Попытка
		ДокОбъект.Записать(РежимЗаписиДокумента.Проведение);
		Возврат "";
	Исключение
		Возврат ОписаниеОшибки();
	КонецПопытки

КонецФункции // ПрименитьНовыеНастройки_Сервер()

&НаКлиенте
Процедура ПрименитьНовыеНастройки(Команда)
	Если ЗначениеЗаполнено(СсылкаНаОбменДанными) Тогда
		ТекстВопроса = "Вы действительно хотите применить новые настройки к обмену данными """+СсылкаНаОбменДанными+"""?";
	Иначе	
		ТекстВопроса = "Вы действительно хотите применить новые общие настройки обменов данными ?";
	КонецЕсли;
	Если Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	ТекстОшибки = ПрименитьНовыеНастройки_Сервер(НовыйДокументИзмененияНастроек);
	Если ПустаяСтрока(ТекстОшибки) Тогда
		ОповеститьОбИзменении(СсылкаНаОбменДанными);
		ПоказатьПредупреждение(,"Настройки успешно применены.");
		ЭтаФорма.Закрыть();
	Иначе
		ПоказатьПредупреждение(,"Дейтвие не выполнено: "+ТекстОшибки);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСравнение_ТолстыйКлиент(Команда)
	
	#Если Не ТолстыйКлиентУправляемоеПриложение Тогда 
		ПоказатьПредупреждение(,"Сравнение текстов средствами 1С возможно только при запуске под толстым клиентом");
		Возврат;
	#КонецЕсли
	
	#Если ТолстыйКлиентУправляемоеПриложение Тогда 
	Т1 = Новый ТекстовыйДокумент();
	Т1.УстановитьТекст(ТекущиеНастройки);
	Имя1 = ПолучитьИмяВременногоФайла("TXT");
	Т1.Записать(Имя1);
	Т2 = Новый ТекстовыйДокумент();
	Т2.УстановитьТекст(НовыеНастройки);
	Имя2 = ПолучитьИмяВременногоФайла("TXT");
	Т2.Записать(Имя2);
	
	Ср = Новый СравнениеФайлов();
	Ср.ПервыйФайл = Имя1;
	Ср.ВторойФайл = Имя2;
	Ср.ИгнорироватьПустоеПространство = Ложь;
	Ср.УчитыватьРазделителиСтрок = Ложь;
	ср.УчитыватьРегистр = Ложь;
	Ср.СпособСравнения = СпособСравненияФайлов.ТекстовыйДокумент;
	Ср.ПоказатьРазличияМодально();
	Попытка
		УдалитьФайлы(Имя1);
		УдалитьФайлы(Имя2);
	Исключение
	;
	КонецПопытки;
	#КонецЕсли
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекущийДокументИзмененияНастроек = Параметры.ДанныеОНастройках.ТекущийДокументНастроек;
	НовыйДокументИзмененияНастроек = Параметры.ДанныеОНастройках.НовыйДокументНастроек;
	ТекущиеНастройки = Параметры.ДанныеОНастройках.ТекущиеНастройки;
	НовыеНастройки = Параметры.ДанныеОНастройках.НовыеНастройки;
	
	СсылкаНаОбменДанными = Параметры.ОбменДанными;
	
	Если ЗначениеЗаполнено(НовыйДокументИзмененияНастроек) Тогда
		Если ЗначениеЗаполнено(СсылкаНаОбменДанными) Тогда
			ЭтаФорма.Заголовок = "Сравнение и применение новых настроек обмена """+СсылкаНаОбменДанными+"""";
		Иначе	
			ЭтаФорма.Заголовок = "Сравнение и применение новых общих настроек обменов";
		КонецЕсли;
		ТекстОшибки = "";
		РаботасОбменомДанными.ПолучитьДанныеXML_ExchangeSettings(НовыеНастройки,ТекстОшибки);
		Если Не ПустаяСтрока(ТекстОшибки) Тогда
			
			Элементы.ТекстОшибки.Видимость = Истина;
			ЭтаФорма.КоманднаяПанель.ПодчиненныеЭлементы.ФормаПоказатьСравнение_ТолстыйКлиент.Доступность = Ложь;
			ЭтаФорма.КоманднаяПанель.ПодчиненныеЭлементы.ФормаПрименитьНовыеНастройки.Доступность = Ложь;
			
		
		КонецЕсли;
	Иначе
		Если ЗначениеЗаполнено(СсылкаНаОбменДанными) Тогда
			ЭтаФорма.Заголовок = "Просмотр настроек обмена """+СсылкаНаОбменДанными+"""";
		Иначе	
			ЭтаФорма.Заголовок = "Просмотр общих настроек обменов";
		КонецЕсли;
		ЭтаФорма.КоманднаяПанель.ПодчиненныеЭлементы.ФормаПоказатьСравнение_ТолстыйКлиент.Видимость = Ложь;
		ЭтаФорма.КоманднаяПанель.ПодчиненныеЭлементы.ФормаПрименитьНовыеНастройки.Видимость = Ложь;
		Элементы.ГруппаНовыхНастроек.Видимость = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если ЗначениеЗаполнено(НовыйДокументИзмененияНастроек) и Не ЭтаФорма.КоманднаяПанель.ПодчиненныеЭлементы.ФормаПрименитьНовыеНастройки.Доступность Тогда
	
		ПоказатьПредупреждение(,"Новые настройки применить нельзя, см. описание ошибки чтения XML");
	
	КонецЕсли;
КонецПроцедуры
