#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Десериализует СправочникОбъект.ТочкиРегистрации из объекта XDTO
// 
// Параметры:
// 	ОбъектОбмена - ОбъектXDTO - элемент справочника в виде объекта XDTO
// 	Версия - Строка - ожидаемая версия объекта обмена
Процедура ЗаполнитьИзОбъектаОбмена(ОбъектОбмена, Версия) Экспорт
	
	ПространствоИмен = СтрШаблон("http://mes.fordsollers.com/ordersRegistrations/%1", Версия);
	Если ЭтотОбъект.ЭтоГруппа Тогда
		ТипОбъектаОбмена = ФабрикаXDTO.Тип(ПространствоИмен, "Справочник.ТочкиРегистрацииГруппа");
		Имя = "Справочник_ТочкиРегистрацииГруппа";
	ИначеЕсли НЕ ЭтотОбъект.ЭтоГруппа Тогда
		ТипОбъектаОбмена = ФабрикаXDTO.Тип(ПространствоИмен, "Справочник.ТочкиРегистрации");
		Имя = "Справочник_ТочкиРегистрации";
	КонецЕсли;
	
	Если ТипЗнч(ОбъектОбмена) = Тип("ОбъектXDTO") И ОбъектОбмена.Тип() = ТипОбъектаОбмена Тогда 
	
		СтруктураОбъекта = ОбменОбъектамиXDTO.ОбъектXDTOВСтруктуру(ОбъектОбмена);
		
		ПравилаКонвертации = ОбменОбъектамиXDTO.ТаблицаПравилКонвертации("ПравилаКонвертацииОбъектовРегистрацииЗаказов");
		ПравилоКонвертации = ОбменОбъектамиXDTO.ПКОПоИмени(ПравилаКонвертации, Имя);
		ОбменОбъектамиXDTO.ОпределитьКлючевыеСвойства(ТипОбъектаОбмена, ПравилоКонвертации);
		
		ОбменОбъектамиXDTO.СтруктураОбъектаXDTOВДанныеИБ(СтруктураОбъекта, ЭтотОбъект, ПравилоКонвертации);

	Иначе
		ВызватьИсключение НСтр("ru = 'Невозможно десериализовать элемент. Неизвестная версия обмена'");
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Иначе
  ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли