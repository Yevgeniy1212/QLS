#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Ищет по уникальному идентификатору ссылку на существующий СправочникОбъект.ТочкиРегистрации .
//  
// Параметры:
// 	ИдентификаторОбъекта - Строка - уникальный идентификатор ссылки элемента справочника
// 	Версия - Строка - номер версии обмена
// Возвращаемое значение:
// 	СправочникСсылка.ТочкиРегистрации - ссылка на существующую точку или пустая ссылка
Функция ПолучитьСсылкуПоИдентификаторуОбмена(ИдентификаторОбъекта, Версия) Экспорт
	Перем ТочкаСсылка;
	
	Если Версия = "2.0" Тогда
		ТочкаСсылка = ПолучитьСсылку(Новый УникальныйИдентификатор(ИдентификаторОбъекта));
	Иначе
		ВызватьИсключение НСтр("ru = 'Невозможно получить ссылку по идентификатору. Неизвестная версия обмена'");
	КонецЕсли;

	Возврат ТочкаСсылка;

КонецФункции

// Создает СправочникОбъект.ТочкиРегистрации по уникальному идентификатору
// 
// Параметры:
// 	ПараметрыЗагрузки - Структура
// Возвращаемое значение:
// 	СправочникОбъект.ТочкиРегистрации -  новый объект справочника точек регистрации
Функция СоздатьЭлементПоИдентификаторуОбмена(ПараметрыЗагрузки) Экспорт
	
	ИдентификаторОбъекта = ПараметрыЗагрузки.ИдентификаторСсылки;
	Версия = ПараметрыЗагрузки.Версия;
	ЭтоГруппа = ПараметрыЗагрузки.ЭтоГруппа;
	
	Если Версия = "2.0" Тогда
		ТочкаСсылка = ПолучитьСсылку(Новый УникальныйИдентификатор(ИдентификаторОбъекта));
		Если ЭтоГруппа Тогда
			ТочкаОбъект = СоздатьГруппу();
		Иначе	
			ТочкаОбъект = СоздатьЭлемент();
		КонецЕсли;
		ТочкаОбъект.УстановитьСсылкуНового(ТочкаСсылка);
		
	Иначе
		ВызватьИсключение НСтр("ru = 'Невозможно создать элемент по идентификатору. Неизвестная версия обмена'");
	КонецЕсли;

	Возврат ТочкаОбъект;

КонецФункции

#КонецОбласти

#КонецЕсли