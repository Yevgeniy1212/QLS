#Область ОбработчикиСобытийФормы
   
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	НастроитьФорму();
КонецПроцедуры     

&НаКлиенте
Процедура ПодборИнспектируемыхЭлементов(Команда)
	
	ПараметрыФормы = Новый Структура("ЗакрыватьПриВыборе, РежимВыбора, МножественныйВыбор", 
		Ложь, // ЗакрыватьПриВыборе 
		Истина, // РежимВыбора 
		Истина); // МножественныйВыбор   
		
	ОткрытьФорму("Справочник.ИнспектируемыеЭлементы.ФормаВыбора", 
		ПараметрыФормы, 
		Элементы.ИнспектируемыеЭлементы,
		,
		,
		,
		, 
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);      
	
КонецПроцедуры

&НаКлиенте
Процедура ИнспектируемыеЭлементыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Массив") Тогда
		Для каждого Значение Из ВыбранноеЗначение Цикл 
			Отбор = Новый Структура("ИнспектируемыйЭлемент", Значение);
			НайденныеСтроки = Объект.ИнспектируемыеЭлементы.НайтиСтроки(Отбор);
			Если НайденныеСтроки.Количество() = 0 Тогда
				НоваяСтрока = Объект.ИнспектируемыеЭлементы.Добавить();
				НоваяСтрока.ИнспектируемыйЭлемент = Значение;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПодборДефектов(Команда)
	
	ПараметрыФормы = Новый Структура("ЗакрыватьПриВыборе, РежимВыбора, МножественныйВыбор", 
		Ложь, // ЗакрыватьПриВыборе 
		Истина, // РежимВыбора 
		Истина); // МножественныйВыбор   
		
	ОткрытьФорму("Справочник.Дефекты.ФормаВыбора", 
		ПараметрыФормы, 
		Элементы.Дефекты,
		,
		,
		,
		, 
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);    
		
КонецПроцедуры

&НаКлиенте
Процедура ДефектыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Массив") Тогда
		Для каждого Значение Из ВыбранноеЗначение Цикл 
			Отбор = Новый Структура("Дефект", Значение);
			НайденныеСтроки = Объект.Дефекты.НайтиСтроки(Отбор);
			Если НайденныеСтроки.Количество() = 0 Тогда
				НоваяСтрока = Объект.Дефекты.Добавить();
				НоваяСтрока.Дефект = Значение;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВсеДефектыПриИзменении(Элемент)
	НастроитьФорму();
КонецПроцедуры

&НаКлиенте
Процедура ВсеИнспектируемыеЭлементыПриИзменении(Элемент)
	НастроитьФорму();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
  
&НаСервере     
Процедура НастроитьФорму()
	Элементы.ИнспектируемыеЭлементы.Доступность = Не Объект.ВсеИнспектируемыеЭлементы;
	Элементы.Дефекты.Доступность = Не Объект.ВсеДефекты;
КонецПроцедуры

#КонецОбласти
