
#Область ПрограммныйИнтерфейс

// Получает из регистра и возвращает ответственного за дефект.
//
// Параметры:
//  ТочкаПроверкиКачества - СправочникСсылка.ТочкиПроверкиКачества - точка проверки качества.
//  ИнспектируемыйЭлемент - СправочникСсылка.ИнспектируемыеЭлементы - инспектируемый элемент.
//  Дефект - СправочникСсылка.Дефекты - дефект.
//  ТипДефекта - СправочникСсылка.ТипыДефектов - тип дефекта.
//
// Возвращаемое значение:
//  СправочникСсылка.ОтветственныйЗаДефект
//
Функция ПолучитьОтветственногоЗаДефект(ТочкаПроверкиКачества, ИнспектируемыйЭлемент, Дефект, ТипДефекта) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Период", ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("ИнспектируемыйЭлемент", ИнспектируемыйЭлемент);
	Запрос.УстановитьПараметр("ТочкаПроверкиКачества", ТочкаПроверкиКачества);
	Запрос.УстановитьПараметр("Дефект", Дефект);
	Запрос.УстановитьПараметр("ТипДефекта", ТипДефекта);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОтветственныеЗаДефектыСрезПоследних.Ответственный КАК Ответственный
		|ИЗ
		|	РегистрСведений.ОтветственныеЗаДефекты.СрезПоследних(
		|			&Период,
		|			ТочкаПроверкиКачества = &ТочкаПроверкиКачества
		|				И ИнспектируемыйЭлемент = &ИнспектируемыйЭлемент
		|				И Дефект = &Дефект
		|				И ТипДефекта = &ТипДефекта) КАК ОтветственныеЗаДефектыСрезПоследних";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Ответственный = Выборка.Ответственный;
	Иначе
		Ответственный = Справочники.ОтветственныйЗаДефект.ПустаяСсылка();
	КонецЕсли;
	
	
	Возврат Ответственный;
	
КонецФункции

#КонецОбласти
