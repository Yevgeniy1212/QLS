#Область ПрограммныйИнтерфейс

// Получает значения параметров из строки.
//
// Параметры:
//  СтрокаПараметров - Строка - строка, содержащая параметры, каждый из которых представляет собой
//                              фрагмент вида <Имя параметра>=<Значение>, где:
//                                Имя параметра — имя параметра; 
//                                Значение — его значение. 
//                              Фрагменты отделяются друг от друга символами ';'.
//                              Если значение содержит пробельные символы, то оно должно быть заключено в двойные кавычки (").
//                              Например:
//                               "File=""c:\InfoBases\Trade""; Usr=""Director"";"
//
// Возвращаемое значение:
//  Структура - структура параметров, где ключ - имя параметра, значение - значение параметра.
//
Функция ПолучитьПараметрыИзСтроки(Знач СтрокаПараметров) Экспорт
	
	Результат = Новый Структура;
	
	СимволДвойныеКавычки = Символ(34); // (")
	
	МассивПодстрок = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтрокаПараметров, ";");
	
	Для Каждого СтрокаПараметра Из МассивПодстрок Цикл
		
		ПозицияПервогоЗнакаРавенства = Найти(СтрокаПараметра, "=");
		
		// Получаем имя параметра
		ИмяПараметра = СокрЛП(Лев(СтрокаПараметра, ПозицияПервогоЗнакаРавенства - 1));
		
		// Получаем значение параметра
		ЗначениеПараметра = СокрЛП(Сред(СтрокаПараметра, ПозицияПервогоЗнакаРавенства + 1));
		
		Если  Лев(ЗначениеПараметра, 1) = СимволДвойныеКавычки
			И Прав(ЗначениеПараметра, 1) = СимволДвойныеКавычки Тогда
			
			ЗначениеПараметра = Сред(ЗначениеПараметра, 2, СтрДлина(ЗначениеПараметра) - 2);
			
		КонецЕсли;
		
		Если Не ПустаяСтрока(ИмяПараметра) Тогда
			
			Результат.Вставить(ИмяПараметра, ЗначениеПараметра);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс
// Код процедур и функций
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьСтрокуБезЛишнихПробелов(ВыбраннаяСтрока) Экспорт
	
	СтрокаРезультат = ВыбраннаяСтрока;	
	
	//Заменяем неразрывный пробел
	СтрокаРезультат = СтрЗаменить(СтрокаРезультат, Символ(160), " ");
	
	//Убираем пробелы в начале и в конце
	СтрокаРезультат = СокрЛП(СтрокаРезультат); 
		
	//Заменяем двойные пробелы на одинарные
	Пока Найти(СтрокаРезультат, "  ") > 0 Цикл		
		СтрокаРезультат = СтрЗаменить(СтрокаРезультат, "  ", " ");		
	КонецЦикла;	
	
	//Заменяем " -" и "- " на "-"
	СтрокаРезультат = СтрЗаменить(СтрокаРезультат, " -", "-");
	СтрокаРезультат = СтрЗаменить(СтрокаРезультат, "- ", "-");
		
	Возврат СтрокаРезультат;
	
КонецФункции

//Возвращает строку, написанную кириллицей в виде латиницы
Функция Транслитерация(Знач Слово) Экспорт
    Слово = ВРег(Слово);
    Слово = ?(Лев(Слово,1) = "Е","YE"+Сред(Слово,2),Слово);
    Слово=СтрЗаменить(Слово,"АЕ","AYE");
    Слово=СтрЗаменить(Слово,"ЕЕ","EYE");
    Пока Найти(Слово,"EЕ") > 0 Цикл
        Слово = СтрЗаменить(Слово,"EЕ","EYE");
    КонецЦикла;
    Слово=СтрЗаменить(Слово,"ЕЁ","YEYO");
    Слово=СтрЗаменить(Слово,"ЁЕ","YOYE");
    Слово=СтрЗаменить(Слово,"ИЕ","IYE");
    Слово=СтрЗаменить(Слово,"ОЕ","OYE");
    Слово=СтрЗаменить(Слово,"УЕ","UYE");
    Слово=СтрЗаменить(Слово,"ЫЕ","YYE");
    Слово=СтрЗаменить(Слово,"ЭЕ","EYE");
    Слово=СтрЗаменить(Слово,"ЮЕ","YUYE");
    Слово=СтрЗаменить(Слово,"ЯЕ","YAYE");
    Слово=СтрЗаменить(Слово,"ЬЕ","YE");
    Слово=СтрЗаменить(Слово,"ЪЕ","YE");
    Слово=СтрЗаменить(Слово,"Ё","YO");
    Слово=СтрЗаменить(Слово,"Ж","ZH");    
    Слово=СтрЗаменить(Слово,"Х","KH");
    Слово=СтрЗаменить(Слово,"Ц","TS");
    Слово=СтрЗаменить(Слово,"Ч","CH");
    Слово=СтрЗаменить(Слово,"Ш","SH");
    Слово=СтрЗаменить(Слово,"Щ","CHSH");
    Слово=СтрЗаменить(Слово,"Ъ","");
    Слово=СтрЗаменить(Слово,"Ь","");
    Слово=СтрЗаменить(Слово,"Ю","YU");
    Слово=СтрЗаменить(Слово,"Я","YA");
    Слово=СтроковыеФункцииКлиентСервер.ЗаменитьОдниСимволыДругими("АБВГДЕЗИЙКЛМНОПРСТУФЫЭ",Слово,"ABVGDEZIJKLMNOPRSTUFYE");
    Возврат Слово;
КонецФункции

//Удалить символы из названия файла для его корректного сохранения
Функция ЗаменитьЗапрещенныеСимволы(Знач ВходящаяСтрока) Экспорт
	
    ЗапрещенныеСимволы = "/\*|`""";//!@/\#$%^&*=|`""
    Для НомерСимвола = 1 По СтрДлина(ЗапрещенныеСимволы) Цикл
        ВходящаяСтрока = СтрЗаменить(ВходящаяСтрока, Сред(ЗапрещенныеСимволы, НомерСимвола, 1), "_")
	КонецЦикла;
	
	Возврат ВходящаяСтрока;
	
КонецФункции

#КонецОбласти