
#Область ПрограммныйИнтерфейс

Функция ДобавитьЗаказыСписком_ОбработкаОповещения(Параметр, ТекстСообщения, Форма) Экспорт
	
	ТекстСообщения = "";
	
	СтруктураВозврата = ПолучитьИзВременногоХранилища(Параметр);
	НеНайденныеVIN = "";
	НомерСТроки = 0;
	
	Если ТипЗнч(СтруктураВозврата) <> Тип("Структура") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	СписокVIN = СтруктураВозврата.ТаблицаДанных.ВыгрузитьКолонку("VIN");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДвижениеЗаказовОстатки.Заказ КАК Заказ,
		|	ДвижениеЗаказовОстатки.ТочкаРегистрации КАК ТочкаРегистрации
		|ИЗ
		|	РегистрНакопления.ДвижениеЗаказов.Остатки(&ТекущаяДата, Заказ.VIN В (&СписокVIN)) КАК ДвижениеЗаказовОстатки
		|ГДЕ
		|	ДвижениеЗаказовОстатки.КоличествоОстаток > 0";

	Запрос.УстановитьПараметр("СписокVIN", СписокVIN);
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	Результат = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = Результат.Выбрать();

	СчетчикДобавленных = 0;
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Если ДобавитьЗаказ(ВыборкаДетальныеЗаписи.Заказ, Форма) Тогда
			СчетчикДобавленных = СчетчикДобавленных + 1;
		КонецЕсли; 
	КонецЦикла;
	
	ТекстСообщения = НСтр("ru='Заказов добавлено: ';en='Orders were added: '") + СчетчикДобавленных;
	
	Если СчетчикДобавленных = 0 Тогда
		Возврат Ложь;
	КонецЕсли; 
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДобавитьЗаказ(Заказ, Форма)
	
	НайденныйЗаказ = Форма.Заказы.НайтиПоЗначению(Заказ);
	
	Если НайденныйЗаказ = Неопределено Тогда
		Форма.Заказы.Добавить(Заказ);
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти
