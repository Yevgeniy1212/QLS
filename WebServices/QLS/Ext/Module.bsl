Функция SubmitQuery(TypeOfExchange, Data)
	
	
	
	ПризнакОкончанияСообщения = Символ(13)+Символ(10);
	NAK = "NAK"+ПризнакОкончанияСообщения;
	НастройкаОбмена = Справочники.НетиповыеОбменыДанными.ПолучитьОбменДляФункцииSubmitQueryСлушающегоВебСервисаПоИдентификатору(TypeOfExchange);
	
	Если НастройкаОбмена = Неопределено Тогда
		ТекстОшибки = "Ошибка вызова веб-сервиса: получено неправильное имя обмена """+TypeOfExchange+""" ( значение параметра с данными: """+Data+""")";
		РегистрыСведений.СОЛЛЕРС_ЖурналРегистрацииСобытий.ЗафиксироватьИнформационноеСообщение(Справочники.СОЛЛЕРС_КлассификаторСобытий.ГлобальнаяОшибкаИнформационнойБазы,Перечисления.СОЛЛЕРС_УровниСобытия.Ошибка,ТекстОшибки);	
		Возврат NAK;
	КонецЕсли;
	
	Попытка
		Возврат ЗапускНетиповогоОбмена(НастройкаОбмена,data);
	Исключение
		Возврат NAK;
	КонецПопытки;
КонецФункции

Функция ЗапускНетиповогоОбмена(НастройкаОбмена,ДопПараметр) Экспорт
	
	// определим событие обмена
	Событие = НастройкаОбмена.СобытиеЖурналаРегистрации;
	Если Не ЗначениеЗаполнено(Событие) Тогда
		ТекстСообщения = "В нетиповом обмене """+НастройкаОбмена.Наименование+""" не указано значение события журнала регистрации.";
		РегистрыСведений.СОЛЛЕРС_ЖурналРегистрацииСобытий.ЗафиксироватьИнформационноеСообщение(Справочники.СОЛЛЕРС_КлассификаторСобытий.ГлобальнаяОшибкаИнформационнойБазы,Перечисления.СОЛЛЕРС_УровниСобытия.Ошибка,ТекстСообщения);	
		ВызватьИсключение ТекстСообщения;
	КонецЕсли; 
	
	// определим обработку обмена
	ИмяОбработкиОбмена = НастройкаОбмена.ИмяОбработкиОбмена;
	Если Не ЗначениеЗаполнено(ИмяОбработкиОбмена) Тогда
		ТекстСообщения = "В нетиповом обмене """+НастройкаОбмена.Наименование+""" не указана обработка обмена";
		РегистрыСведений.СОЛЛЕРС_ЖурналРегистрацииСобытий.ЗафиксироватьИнформационноеСообщение(Событие,Перечисления.СОЛЛЕРС_УровниСобытия.Ошибка,ТекстСообщения);	
		ВызватьИсключение ТекстСообщения;
	КонецЕсли; 
	
	Попытка
		ОбработкаОбмена = Обработки[ИмяОбработкиОбмена];
	Исключение
		ТекстСообщения = "В нетиповом обмене """+НастройкаОбмена.Наименование+""" указана несуществующая обработка обмена """+ИмяОбработкиОбмена+"""";
		РегистрыСведений.СОЛЛЕРС_ЖурналРегистрацииСобытий.ЗафиксироватьИнформационноеСообщение(Событие,Перечисления.СОЛЛЕРС_УровниСобытия.Ошибка,ТекстСообщения);	
		ВызватьИсключение ТекстСообщения;
	КонецПопытки;
	
	Возврат РаботаСОбменомДанными.ВыполнениеОбмена(ОбработкаОбмена,НастройкаОбмена,ДопПараметр,Событие);
КонецФункции

