////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

Процедура СформироватьОтчет(ТабДокумент,ВариантОтчета,ДатаОтчета, ТекстОшибки ="", ПараметрыВывода = Неопределено) Экспорт		
	
	Макет = Отчеты.ОтчетПоОткрытиюКонсерновWEB.ПолучитьМакет("Макет");
	ТекстОшибки = "";
	ТабДокумент.Очистить();
	Если НЕ ЗначениеЗаполнено(ДатаОтчета)Тогда
		ДатаОтчета = ТекущаяДата();
	КонецЕсли; 
	Если ВариантОтчета.ПериодОтчета = 1 Тогда
		НачалоДатыОтчета = НачалоДня(ДатаОтчета);
		КонецДатыОтчета = КонецДня(ДатаОтчета);
	ИначеЕсли ВариантОтчета.ПериодОтчета = 2 Тогда
		НачалоДатыОтчета = НачалоНедели(ДатаОтчета);
		КонецДатыОтчета = КонецНедели(ДатаОтчета);
	Иначе
		НачалоДатыОтчета = НачалоМесяца(ДатаОтчета);
		КонецДатыОтчета = КонецМесяца(ДатаОтчета);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КонсерныОстаткиИОбороты.Период,
		|	КонсерныОстаткиИОбороты.Заказ,
		|	КонсерныОстаткиИОбороты.Консерн,
		|	Консерны.ИнспектируемыйЭлемент,
		|	Консерны.Дефект
		|ПОМЕСТИТЬ Открытые
		|ИЗ
		|	РегистрНакопления.Консерны.ОстаткиИОбороты(&НачалоПериода, &КонецПериода, Регистратор, , ) КАК КонсерныОстаткиИОбороты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.Консерны КАК Консерны
		|		ПО КонсерныОстаткиИОбороты.Регистратор = Консерны.Регистратор
		|			И КонсерныОстаткиИОбороты.Период = Консерны.Период
		|			И КонсерныОстаткиИОбороты.Заказ = Консерны.Заказ
		|			И КонсерныОстаткиИОбороты.Консерн = Консерны.Консерн
		|ГДЕ
		|	КонсерныОстаткиИОбороты.КоличествоПриход > 0
		|	И Консерны.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И Консерны.ИнспектируемыйЭлемент В(&ИнспектируемыйЭлемент)
		|	И Консерны.ТочкаПроверкиКачества В(&ТочкаПроверкиКачества)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Открытые.Заказ,
		|	Открытые.Период КАК ДатаОткрытия,
		|	Открытые.ИнспектируемыйЭлемент.Наименование + "" - "" + Открытые.Дефект.Наименование КАК Консерн,
		|	ВЫБОР
		|		КОГДА КонсерныОбороты.Заказ ЕСТЬ NULL 
		|			ТОГДА ""Open""
		|		ИНАЧЕ ""Closed""
		|	КОНЕЦ КАК Статус,
		|	Открытые.ИнспектируемыйЭлемент,
		|	Открытые.Дефект
		|ПОМЕСТИТЬ ВыходнаяТаблица
		|ИЗ
		|	Открытые КАК Открытые
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Консерны.Обороты(&НачалоПериода, &КонецПериода, , ) КАК КонсерныОбороты
		|		ПО Открытые.Заказ = КонсерныОбороты.Заказ
		|			И Открытые.Консерн = КонсерныОбороты.Консерн
		|			И (КонсерныОбороты.КоличествоРасход > 0)
		|ГДЕ
		|	((&ОтображаемыйСтатус = 1
		|				ИЛИ &ОтображаемыйСтатус = 2)
		|				И КонсерныОбороты.Заказ ЕСТЬ NULL 
		|			ИЛИ (&ОтображаемыйСтатус = 1
		|				ИЛИ &ОтображаемыйСтатус = 3)
		|				И НЕ КонсерныОбороты.Заказ ЕСТЬ NULL )
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ВыходнаяТаблица.ИнспектируемыйЭлемент,
		|	ВыходнаяТаблица.Дефект,
		|	КОЛИЧЕСТВО(ВыходнаяТаблица.Заказ) КАК КоличествоОбщ
		|ПОМЕСТИТЬ ПервыеДефекты
		|ИЗ
		|	ВыходнаяТаблица КАК ВыходнаяТаблица
		|
		|СГРУППИРОВАТЬ ПО
		|	ВыходнаяТаблица.ИнспектируемыйЭлемент,
		|	ВыходнаяТаблица.Дефект
		|
		|УПОРЯДОЧИТЬ ПО
		|	КоличествоОбщ УБЫВ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВыходнаяТаблица.Заказ КАК Заказ,
		|	ВыходнаяТаблица.ДатаОткрытия КАК ДатаОткрытия,
		|	ВыходнаяТаблица.Консерн КАК Консерн,
		|	ВыходнаяТаблица.ИнспектируемыйЭлемент КАК ИнспектируемыйЭлемент,
		|	ВыходнаяТаблица.Дефект КАК Дефект,
		|	ВыходнаяТаблица.Статус,
		|	ПервыеДефекты.КоличествоОбщ КАК КоличествоОбщ
		|ИЗ
		|	ПервыеДефекты КАК ПервыеДефекты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВыходнаяТаблица КАК ВыходнаяТаблица
		|		ПО ПервыеДефекты.ИнспектируемыйЭлемент = ВыходнаяТаблица.ИнспектируемыйЭлемент
		|			И ПервыеДефекты.Дефект = ВыходнаяТаблица.Дефект
		|
		|УПОРЯДОЧИТЬ ПО
		|	КоличествоОбщ УБЫВ,
		|	ДатаОткрытия
		|ИТОГИ
		|	КОЛИЧЕСТВО(Заказ)
		|ПО
		|	Консерн";
	Запрос.УстановитьПараметр("НачалоПериода", НачалоДатыОтчета);
	Запрос.УстановитьПараметр("КонецПериода", КонецДатыОтчета);
	Запрос.УстановитьПараметр("ОтображаемыйСтатус", ВариантОтчета.ОтображаемыеСтатусы);
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1",?(ВариантОтчета.КоличествоВТОП=0,"","ПЕРВЫЕ "+ВариантОтчета.КоличествоВТОП));
	Если ВариантОтчета.ИнспектируемыеЭлементы.Количество()<>0 Тогда
		Запрос.УстановитьПараметр("ИнспектируемыйЭлемент", ВариантОтчета.ИнспектируемыеЭлементы.Выгрузить(,"ИнспектируемыйЭлемент"));
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И Консерны.ИнспектируемыйЭлемент В(&ИнспектируемыйЭлемент)","");
	КонецЕсли;
	Если ВариантОтчета.ТочкиПроверкиКачества.Количество()<>0 Тогда
		Запрос.УстановитьПараметр("ТочкаПроверкиКачества", ВариантОтчета.ТочкиПроверкиКачества.Выгрузить(,"ТочкаПроверкиКачества"));
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И Консерны.ТочкаПроверкиКачества В(&ТочкаПроверкиКачества)","");
	КонецЕсли;
	
	Результат = Запрос.Выполнить();
	//Выведем шапку отчета
	СекцияШапка = Макет.ПолучитьОбласть("Шапка");
	СекцияШапка.Параметры.ЗаголовокОтчета = ВариантОтчета.Наименование+ " на " + ТекущаяДата();
	ТабДокумент.Вывести(СекцияШапка);
	СекцияШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ТабДокумент.Вывести(СекцияШапкаТаблицы);

	ВыборкаДефект = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	Пока ВыборкаДефект.Следующий() Цикл
		СекцияСтрока = Макет.ПолучитьОбласть("СтрокаТаблицыКонсерн");
		ЗаполнитьЗначенияСвойств(СекцияСтрока.Параметры,ВыборкаДефект);
		СекцияСтрока.Параметры.ОбщКолво = ВыборкаДефект.Заказ;
		ТабДокумент.Вывести(СекцияСтрока);
		
		ВыборкаДетальныеЗаписи = ВыборкаДефект.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			СекцияСтрока = Макет.ПолучитьОбласть("СтрокаТаблицы");
			ЗаполнитьЗначенияСвойств(СекцияСтрока.Параметры,ВыборкаДетальныеЗаписи);
			ТабДокумент.Вывести(СекцияСтрока);
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры

Функция ПолучитьОписаниеПроизводственныхОтчетов() Экспорт
	 
	 СтруктураНастроек = Справочники.НастройкаОтчетов_MesReports.ПолучитьОписаниеПроизводственныхОтчетовПоУмолчанию();
	 
	 СтруктураНастроек.МассивТиповВариантовОтчета.Добавить(Тип("СправочникСсылка.ВариантыНастроекОтчетаПоОткрытиюКонсерновWEB"));
	 	 
	 Возврат СтруктураНастроек;
	 
 КонецФункции
//
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

