#Если Сервер Или ТолстыйКлиентОбычноеПриложение Тогда
	
#Область ОбработчикиСобытий

Функция GetResultGET(HTTPЗапрос) Экспорт
		
	СхемаПараметров = Новый Соответствие;
	СхемаПараметров.Вставить("orderVIN", Новый Структура("Тип, Обязательное, Пустое", Тип("Строка"), Истина, Ложь));
	СхемаПараметров.Вставить("ver", Новый Структура("Тип, Обязательное, Равно", Тип("Строка"), Истина, "1.0"));
	СхемаПараметров.Вставить("poolId", Новый Структура("Тип, Обязательное, Пустое", Тип("Строка"), Истина, Ложь));
	
	Запрос = ВебПриложениеРасширяемый.НовыйНТТРЗапрос(HTTPЗапрос);
	Запрос.ДополнительныеСвойства.Вставить("СхемаПараметров", СхемаПараметров);
	
	Ответ = ВебПриложениеРасширяемый.НовыйНТТРОтвет();
	Ответ.ТипСодержимого("application/json");
	
	HTTPСервисыСервер.ОбъединитьПараметрыЗапроса(Запрос, Ответ);	
	HTTPСервисыСервер.ПроверитьПараметрыЗапроса(Запрос, Ответ);	
	
	НайтиЗаказПоVIN(Запрос, Ответ);	
	НайтиПулПоИдентификатору(Запрос, Ответ);
	ЗаполнитьРезультатПроверкиКачества(Запрос, Ответ);
	
	HTTPСервисыСервер.ПреобразоватьОшибку(Запрос, Ответ);
	HTTPСервисыСервер.СериализоватьОтвет(Запрос, Ответ);
	ВебПриложениеРасширяемый.ЗаписатьВЖурнал(Запрос, Ответ);
		
	Возврат Ответ.Получить();
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура НайтиЗаказПоVIN(Запрос, Ответ)
	
	Если Ответ.Готов() Тогда
		Возврат;
	КонецЕсли;
	
	VIN = Запрос.ДополнительныеСвойства.Параметры.Получить("orderVIN");
	Заказ = Справочники.Заказы.НайтиПоVINНомеру(VIN);
	Если Не ЗначениеЗаполнено(Заказ) Тогда
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Не найден заказ по VIN %1'; en = 'Order not found by VIN %1'"), VIN);
		Ошибка = ВебПриложениеРасширяемый.НовыйОшибка(404, ТекстОшибки);
		Ответ.Отправить(Ошибка)
	КонецЕсли;
	
	Запрос.ДополнительныеСвойства.Вставить("Заказ", Заказ);

КонецПроцедуры

Процедура НайтиПулПоИдентификатору(Запрос, Ответ)
	
	Если Ответ.Готов() Тогда
		Возврат;
	КонецЕсли;
	
	Идентификатор = Запрос.ДополнительныеСвойства.Параметры.Получить("poolId");
	Если Не ЗначениеЗаполнено(Идентификатор) Тогда
		Возврат;	
	КонецЕсли;
	
	Пул = Справочники.ПулыДанныхДляКонтроля.НайтиПоИдентификатору(Идентификатор);
	Если Не ЗначениеЗаполнено(Пул) Тогда
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Не найден пул по ID %1';
			|en = 'Pool not found by ID %1'"), Идентификатор);
		Ошибка = ВебПриложениеРасширяемый.НовыйОшибка(400, ТекстОшибки);
		Ответ.Отправить(Ошибка)
	КонецЕсли;
	
	Запрос.ДополнительныеСвойства.Вставить("Пул", Пул);
	
КонецПроцедуры

Процедура ЗаполнитьРезультатПроверкиКачества(Запрос, Ответ)
	
	Перем Заказ;
	Перем Пул;
	
	Если Ответ.Готов() Тогда
		Возврат;
	КонецЕсли;
	
	Запрос.ДополнительныеСвойства.Свойство("Заказ", Заказ);
	Запрос.ДополнительныеСвойства.Свойство("Пул", Пул);
	
	СообщениеОбОшибке = "";
	ВыполняетсяКонтроль = Справочники.ПулыДанныхДляКонтроля.ВыполняетсяКонтроль(Заказ, Пул, СообщениеОбОшибке);
	
	Данные = Новый Структура;
	Данные.Вставить("isSuccess", ВыполняетсяКонтроль);
	Данные.Вставить("userMessage", СообщениеОбОшибке);
	
	Ответ.Отправить(Данные);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли