
Функция ПолучитьОписаниеОбработкиОбмена() Экспорт
	
	// полный состав параметров описания смотри в этой функции
	СтруктураНастроек = Справочники.НетиповыеОбменыДанными.ПолучитьОписаниеОбработкиОбменаПоУмолчанию();
	
	СтруктураНастроек.ТекстовоеОписаниеОбмена = НСтр("ru='Обмен по рассылке писем при срабатывании тресхолдов.';en='The exchange for sending emails after actuation of thresholds'");
	СтруктураНастроек.ВидОбмена = Перечисления.ВидыОбмена.Выгрузка;
	СтруктураНастроек.СпособПередачиДанных = Перечисления.СпособыПередачиДанныхВНетиповыхОбменах.None;
	СтруктураНастроек.ИспользованиеРегламентогоЗадания = Истина;

	Возврат СтруктураНастроек;
КонецФункции // ()

Функция ВыполнитьОбмен(Событие, НастройкаОбмена) Экспорт
	ТекущаяДата = ТекущаяДата();   

	СообщениеОшибки = "";
	ПороговыеЗначения = ПолучитьПороговыеЗначенияИТаблицуСПериодамиТресхолдов();
	
	//В базе не настроены тресхолды
	Если ПороговыеЗначения = Неопределено Тогда
		Возврат "";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТабТресхолдПериоды.ТресхолдСсылка КАК ТресхолдСсылка,
		|	ТабТресхолдПериоды.ДатаНачала КАК ДатаНачала,
		|	ТабТресхолдПериоды.ДатаОкончания КАК ДатаОкончания
		|ПОМЕСТИТЬ ТабТресхолдПериоды
		|ИЗ
		|	&ТабТресхолдПериоды КАК ТабТресхолдПериоды
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Тресхолды.Ссылка КАК ТресхолдСсылка,
		|	Тресхолды.ГруппаИзмеренияКоличестваКонсернов КАК ГруппаИзмеренияКоличестваКонсерновСсылка,
		|	ТабТресхолдПериоды.ДатаНачала КАК ДатаНачала,
		|	ТабТресхолдПериоды.ДатаОкончания КАК ДатаОкончания
		|ПОМЕСТИТЬ СписокВсехТресхолдов
		|ИЗ
		|	Справочник.Тресхолды КАК Тресхолды
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТабТресхолдПериоды КАК ТабТресхолдПериоды
		|		ПО Тресхолды.Ссылка = ТабТресхолдПериоды.ТресхолдСсылка
		|ГДЕ
		|	НЕ Тресхолды.Архив
		|	И НЕ Тресхолды.ПометкаУдаления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СписокВсехТресхолдов.ТресхолдСсылка КАК ТресхолдСсылка,
		|	СписокВсехТресхолдов.ГруппаИзмеренияКоличестваКонсерновСсылка КАК ГруппаИзмеренияКоличестваКонсерновСсылка,
		|	ГруппыИзмеренияКоличестваКонсерновИнспектируемыеЭлементы.ИнспектируемыйЭлемент КАК ИнспектируемыйЭлементСсылка
		|ПОМЕСТИТЬ СписокИЭ
		|ИЗ
		|	СписокВсехТресхолдов КАК СписокВсехТресхолдов
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ГруппыИзмеренияКоличестваКонсернов.ИнспектируемыеЭлементы КАК ГруппыИзмеренияКоличестваКонсерновИнспектируемыеЭлементы
		|		ПО СписокВсехТресхолдов.ГруппаИзмеренияКоличестваКонсерновСсылка = ГруппыИзмеренияКоличестваКонсерновИнспектируемыеЭлементы.Ссылка
		|ГДЕ
		|	НЕ ГруппыИзмеренияКоличестваКонсерновИнспектируемыеЭлементы.ИнспектируемыйЭлемент.ПометкаУдаления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СписокВсехТресхолдов.ТресхолдСсылка КАК ТресхолдСсылка,
		|	СписокВсехТресхолдов.ГруппаИзмеренияКоличестваКонсерновСсылка КАК ГруппаИзмеренияКоличестваКонсерновСсылка,
		|	ГруппыИзмеренияКоличестваКонсерновТочкиПроверкиКачества.ТочкаПроверкиКачества КАК ТочкаПроверкиКачестваСсылка
		|ПОМЕСТИТЬ СписокТочекПроверкиКачества
		|ИЗ
		|	СписокВсехТресхолдов КАК СписокВсехТресхолдов
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ГруппыИзмеренияКоличестваКонсернов.ТочкиПроверкиКачества КАК ГруппыИзмеренияКоличестваКонсерновТочкиПроверкиКачества
		|		ПО СписокВсехТресхолдов.ГруппаИзмеренияКоличестваКонсерновСсылка = ГруппыИзмеренияКоличестваКонсерновТочкиПроверкиКачества.Ссылка
		|ГДЕ
		|	НЕ ГруппыИзмеренияКоличестваКонсерновТочкиПроверкиКачества.ТочкаПроверкиКачества.ПометкаУдаления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СписокВсехТресхолдов.ТресхолдСсылка КАК ТресхолдСсылка,
		|	СписокВсехТресхолдов.ГруппаИзмеренияКоличестваКонсерновСсылка КАК ГруппаИзмеренияКоличестваКонсерновСсылка,
		|	ГруппыИзмеренияКоличестваКонсерновДефекты.Дефект КАК ДефектСсылка
		|ПОМЕСТИТЬ СписокДефектов
		|ИЗ
		|	СписокВсехТресхолдов КАК СписокВсехТресхолдов
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ГруппыИзмеренияКоличестваКонсернов.Дефекты КАК ГруппыИзмеренияКоличестваКонсерновДефекты
		|		ПО СписокВсехТресхолдов.ГруппаИзмеренияКоличестваКонсерновСсылка = ГруппыИзмеренияКоличестваКонсерновДефекты.Ссылка
		|ГДЕ
		|	НЕ ГруппыИзмеренияКоличестваКонсерновДефекты.Дефект.ПометкаУдаления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СписокВсехТресхолдов.ТресхолдСсылка КАК ТресхолдСсылка,
		|	СписокВсехТресхолдов.ГруппаИзмеренияКоличестваКонсерновСсылка КАК ГруппаИзмеренияКоличестваКонсерновСсылка,
		|	ЕСТЬNULL(СписокИЭ.ИнспектируемыйЭлементСсылка, ЗНАЧЕНИЕ(Справочник.ИнспектируемыеЭлементы.ПустаяСсылка)) КАК ИнспектируемыйЭлементСсылка,
		|	ЕСТЬNULL(СписокТочекПроверкиКачества.ТочкаПроверкиКачестваСсылка, ЗНАЧЕНИЕ(Справочник.ТочкиПроверкиКачества.ПустаяСсылка)) КАК ТочкаПроверкиКачестваСсылка,
		|	ЕСТЬNULL(СписокДефектов.ДефектСсылка, ЗНАЧЕНИЕ(Справочник.Дефекты.ПустаяСсылка)) КАК ДефектСсылка,
		|	СписокВсехТресхолдов.ДатаНачала КАК ДатаНачала,
		|	СписокВсехТресхолдов.ДатаОкончания КАК ДатаОкончания
		|ПОМЕСТИТЬ ПолнаяТаблицаГруппаИзмерений
		|ИЗ
		|	СписокВсехТресхолдов КАК СписокВсехТресхолдов
		|		ЛЕВОЕ СОЕДИНЕНИЕ СписокИЭ КАК СписокИЭ
		|		ПО СписокВсехТресхолдов.ТресхолдСсылка = СписокИЭ.ТресхолдСсылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ СписокТочекПроверкиКачества КАК СписокТочекПроверкиКачества
		|		ПО СписокВсехТресхолдов.ТресхолдСсылка = СписокТочекПроверкиКачества.ТресхолдСсылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ СписокДефектов КАК СписокДефектов
		|		ПО СписокВсехТресхолдов.ТресхолдСсылка = СписокДефектов.ТресхолдСсылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КонсерныОбороты.Консерн КАК Консерн,
		|	КонсерныОбороты.Консерн.ИнспектируемыйЭлемент КАК КонсернИнспектируемыйЭлемент,
		|	КонсерныОбороты.Консерн.ТочкаПроверкиКачества КАК КонсернТочкаПроверкиКачества,
		|	КонсерныОбороты.Консерн.Дефект КАК КонсернДефект,
		|	КонсерныОбороты.Регистратор.Дата КАК РегистраторДата
		|ПОМЕСТИТЬ КонсерныПоГруппеИзмерений
		|ИЗ
		|	РегистрНакопления.Консерны.Обороты(
		|			&НачалоПериода,
		|			,
		|			Запись,
		|			Консерн.ИнспектируемыйЭлемент В
		|					(ВЫБРАТЬ
		|						СписокИЭ.ИнспектируемыйЭлементСсылка КАК ИнспектируемыйЭлементСсылка
		|					ИЗ
		|						СписокИЭ КАК СписокИЭ)
		|				ИЛИ Консерн.Дефект В
		|					(ВЫБРАТЬ
		|						СписокДефектов.ДефектСсылка КАК ДефектСсылка
		|					ИЗ
		|						СписокДефектов КАК СписокДефектов)
		|				ИЛИ Консерн.ТочкаПроверкиКачества В
		|					(ВЫБРАТЬ
		|						СписокТочекПроверкиКачества.ТочкаПроверкиКачестваСсылка КАК ТочкаПроверкиКачестваСсылка
		|					ИЗ
		|						СписокТочекПроверкиКачества КАК СписокТочекПроверкиКачества)) КАК КонсерныОбороты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПолнаяТаблицаГруппаИзмерений.ТресхолдСсылка КАК ТресхолдСсылка,
		|	КОЛИЧЕСТВО(КонсерныПоГруппеИзмерений.Консерн) КАК КоличествоКонсернов
		|ПОМЕСТИТЬ ТресхолдыСКоличествомКонсернов
		|ИЗ
		|	ПолнаяТаблицаГруппаИзмерений КАК ПолнаяТаблицаГруппаИзмерений
		|		ЛЕВОЕ СОЕДИНЕНИЕ КонсерныПоГруппеИзмерений КАК КонсерныПоГруппеИзмерений
		|		ПО (ВЫБОР
		|				КОГДА ПолнаяТаблицаГруппаИзмерений.ИнспектируемыйЭлементСсылка <> ЗНАЧЕНИЕ(Справочник.ИнспектируемыеЭлементы.ПустаяСсылка)
		|					ТОГДА ПолнаяТаблицаГруппаИзмерений.ИнспектируемыйЭлементСсылка = КонсерныПоГруппеИзмерений.Консерн.ИнспектируемыйЭлемент
		|				ИНАЧЕ ИСТИНА
		|			КОНЕЦ)
		|			И (ВЫБОР
		|				КОГДА ПолнаяТаблицаГруппаИзмерений.ТочкаПроверкиКачестваСсылка <> ЗНАЧЕНИЕ(Справочник.ТочкиПроверкиКачества.ПустаяСсылка)
		|					ТОГДА ПолнаяТаблицаГруппаИзмерений.ТочкаПроверкиКачестваСсылка = КонсерныПоГруппеИзмерений.КонсернТочкаПроверкиКачества
		|				ИНАЧЕ ИСТИНА
		|			КОНЕЦ)
		|			И (ВЫБОР
		|				КОГДА ПолнаяТаблицаГруппаИзмерений.ДефектСсылка <> ЗНАЧЕНИЕ(Справочник.Дефекты.ПустаяСсылка)
		|					ТОГДА ПолнаяТаблицаГруппаИзмерений.ДефектСсылка = КонсерныПоГруппеИзмерений.КонсернДефект
		|				ИНАЧЕ ИСТИНА
		|			КОНЕЦ)
		|			И (ВЫБОР
		|				КОГДА КонсерныПоГруппеИзмерений.РегистраторДата МЕЖДУ ПолнаяТаблицаГруппаИзмерений.ДатаНачала И ПолнаяТаблицаГруппаИзмерений.ДатаОкончания
		|					ТОГДА ИСТИНА
		|				ИНАЧЕ ЛОЖЬ
		|			КОНЕЦ)
		|
		|СГРУППИРОВАТЬ ПО
		|	ПолнаяТаблицаГруппаИзмерений.ТресхолдСсылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТресхолдыСКоличествомКонсернов.ТресхолдСсылка КАК ТресхолдСсылка,
		|	ТресхолдыСКоличествомКонсернов.КоличествоКонсернов КАК КоличествоКонсернов,
		|	ТресхолдыСКоличествомКонсернов.ТресхолдСсылка.Уровень КАК ТресхолдУровень,
		|	ТресхолдыСКоличествомКонсернов.ТресхолдСсылка.СобытиеЖурналаРегистрацииПриОтправкеПоЭлектроннойПочте КАК СобытиеЖурналаРегистрацииПриОтправкеПоЭлектроннойПочте,
		|	ТресхолдыСКоличествомКонсернов.ТресхолдСсылка.Наименование КАК ТресхолдНаименование
		|ИЗ
		|	ТресхолдыСКоличествомКонсернов КАК ТресхолдыСКоличествомКонсернов";
		
	Запрос.УстановитьПараметр("НачалоПериода", ПороговыеЗначения.МинДата);
	Запрос.УстановитьПараметр("ТабТресхолдПериоды", ПороговыеЗначения.ТабТресхолдПериоды);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		// если мы достигли указанного уровня по тресхолду, то в ЖРС записывается сообщение для отправки по почте, а в спец. регистр - 
		// запись о том, что по данному тресхолду сообщение уже было зафиксировано
		Если Выборка.КоличествоКонсернов >= Выборка.ТресхолдУровень Тогда
			ТекущаяДата = ТекущаяДата();
			ТекстСообщения = НСтр("ru='Количество консернов по тресхолду ""';en='Amount of concerns of threshold ""'") + Выборка.ТресхолдНаименование + НСтр("ru='"" на дату ';en='"" on the date '") + Формат(ТекущаяДата, "ДФ=""дд/ММ-гггг""") + НСтр("ru=' достигло обозначенного уровня ';en=' reached defined level '") + Выборка.ТресхолдУровень;
			НачатьТранзакцию();
			Попытка
				РегистрыСведений.СОЛЛЕРС_ЖурналРегистрацииСобытий.ЗафиксироватьИнформационноеСообщение(Выборка.СобытиеЖурналаРегистрацииПриОтправкеПоЭлектроннойПочте, Перечисления.СОЛЛЕРС_УровниСобытия.Информация, ТекстСообщения,,,,"Превышение уровня консернов");	
				НаборРегистра = РегистрыСведений.ТресхолдыСработавшиеЗаАнализируемыйПериод.СоздатьНаборЗаписей();
				НаборРегистра.Отбор.Тресхолд.Установить(Выборка.ТресхолдСсылка);
				НоваяЗапись = НаборРегистра.Добавить();
				НоваяЗапись.Тресхолд = Выборка.ТресхолдСсылка;
				НоваяЗапись.КоличествоКонсернов = Выборка.КоличествоКонсернов;
				НоваяЗапись.ТресхолдУровень = Выборка.ТресхолдУровень;
				НоваяЗапись.Период = ТекущаяДата; 
				НаборРегистра.Записать();
				ЗафиксироватьТранзакцию();
			Исключение
				ОтменитьТранзакцию();
				СообщениеОшибки = НСтр("ru='Неудачная попытка записи по тресхолду ""';en='Unsuccessful attempt of writing of threshold ""'") + Выборка.ТресхолдНаименование + НСтр("ru='"" достигшего обозначенный уровень консернов.';en='"" which reached defined level of concern.'") + ОписаниеОшибки();
				РегистрыСведений.СОЛЛЕРС_ЖурналРегистрацииСобытий.ЗафиксироватьИнформационноеСообщение(Событие, Перечисления.СОЛЛЕРС_УровниСобытия.Ошибка, СообщениеОшибки);	
			КонецПопытки;
		КонецЕсли;
	КонецЦикла;
	
	Возврат СообщениеОшибки;
	
КонецФункции

//Функция возвращает структуру с мин датой по тресхолдам и
//таблицу с тресхолдами и их периодами для анализа для выборки консернов
Функция ПолучитьПороговыеЗначенияИТаблицуСПериодамиТресхолдов()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Тресхолды.ВариантАнализируемогоПериода КАК ВариантАнализируемогоПериода,
	|	Тресхолды.ДатаНачала КАК ДатаНачала,
	|	Тресхолды.ДатаОкончания КАК ДатаОкончания,
	|	Тресхолды.Ссылка КАК ТресхолдСсылка
	|ИЗ
	|	Справочник.Тресхолды КАК Тресхолды
	|ГДЕ
	|	НЕ Тресхолды.Архив
	|	И НЕ Тресхолды.ПометкаУдаления";
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	
	ПервыйПроход = Ложь;
	
	ТабТресхолдПериоды = Новый ТаблицаЗначений();
	ТабТресхолдПериоды.Колонки.Добавить("ТресхолдСсылка", Новый ОписаниеТипов("СправочникСсылка.Тресхолды"));
	ТабТресхолдПериоды.Колонки.Добавить("ДатаНачала", Новый ОписаниеТипов("Дата"));
	ТабТресхолдПериоды.Колонки.Добавить("ДатаОкончания", Новый ОписаниеТипов("Дата"));
	
	Пока Выборка.Следующий() Цикл
		
		Вариант = Выборка.ВариантАнализируемогоПериода.Получить(); //Может быть незаполнен (наприме, новый объект)
		Если Вариант <> Неопределено Тогда
			ВариантАнализируемогоПериода = Новый СтандартныйПериод(Вариант);	
		КонецЕсли;
		
		//Если у нас вариант произвольный, значит у него в тресхолде должны быть заполнены дата начала и окончания
		//Иначе берем из варианта период (наприме ЭтотМесяц или Завтра и тд)
		Если СлужебныеПроцедурыФункции.ПроизвольныйЛиВариантУАнализируемогоПериода(ВариантАнализируемогоПериода.Вариант) Тогда
			НижнийПорог = Выборка.ДатаНачала;
			
			Если НЕ ЗначениеЗаполнено(Выборка.ДатаОкончания) Тогда  //Не указали дату окончания тресхолда, действует вечно
				ВерхнийПорог = КонецДня(ТекущаяДата());
			Иначе
				ВерхнийПорог = Выборка.ДатаОкончания;
			КонецЕсли;
			
		Иначе
			НижнийПорог = ВариантАнализируемогоПериода.ДатаНачала;
			ВерхнийПорог = ВариантАнализируемогоПериода.ДатаОкончания;
		КонецЕсли;
		
		//Добавим в ТабТресхолдПериоды данные
		НоваяСтр = ТабТресхолдПериоды.Добавить();
		НоваяСтр.ТресхолдСсылка = Выборка.ТресхолдСсылка;
		НоваяСтр.ДатаНачала = НижнийПорог;
		НоваяСтр.ДатаОкончания = ВерхнийПорог;
		
		//Присвоим мин и макс первые значения
		Если Не ПервыйПроход Тогда
			МинДата = НижнийПорог;
			ПервыйПроход = Истина;
		КонецЕсли;

		//Мин и Макс
		Если МинДата > НижнийПорог Тогда
			МинДата = НижнийПорог;
		КонецЕсли;
			
	КонецЦикла;
	
	ПорогиСтруктура = Новый Структура("МинДата, ТабТресхолдПериоды", МинДата, ТабТресхолдПериоды);  
	
	Возврат ПорогиСтруктура;
	
КонецФункции
