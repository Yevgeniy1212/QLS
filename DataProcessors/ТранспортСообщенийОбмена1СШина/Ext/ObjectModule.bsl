#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

Перем ТипСодержимого; // Строка 
Перем ИмяКанала; // Строка 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Выполняет инициализацию свойств обработки начальными значениями и константами.
// 
// Параметры:
//  НастройкиТранспортаОбменаДанными - Структура - Настройки транспорта обмена данными
//   
// Возвращаемое значение:
//  ОбработкаОбъект.ТранспортСообщенийОбмена1СШина
Функция Инициализировать(НастройкиТранспортаОбменаДанными) Экспорт
	
	ТипСодержимого = "application/json";
	ИмяКанала = НастройкиТранспортаОбменаДанными.ИмяФайлаВыгрузки;
	
	Возврат ЭтотОбъект;
	
КонецФункции

// Выполняет отправку сообщения обмена 
// 
// Параметры:
//  ДанныеСобытия - Строка - Информация о событии
//  ОтправляемыйОбъект - ЛюбаяСсылка - Отправляемый объект
// 
// Возвращаемое значение:
//  Строка - Ответ
Функция ОтправитьСообщение(ДанныеСобытия, ОтправляемыйОбъект) Экспорт
	
	Возврат ОтправитьСообщение1СШина(ДанныеСобытия);
	
КонецФункции

// Отправка данных в 1С шину
// 
// Параметры:
//  СообщениеОбмена - Строка - Текст сообщения
// 
// Возвращаемое значение:
//  Строка  - ответ от узла
Функция ОтправитьСообщение1СШина(СообщениеОбмена)
	
	Попытка
		ИсходящееСообщение = СервисыИнтеграции.QLSDataExchange.СоздатьСообщение();
		ИсходящееСообщение.ИдентификаторСообщенияЗапроса = Новый УникальныйИдентификатор;
		ИсходящееСообщение.Параметры.Вставить("Content-Type", ТипСодержимого);
		
		Тело = ИсходящееСообщение.ПолучитьТелоКакПоток();
		Буфер = ПолучитьБуферДвоичныхДанныхИзСтроки(СообщениеОбмена, КодировкаТекста.UTF8);
		Тело.Записать(Буфер, 0, Буфер.Размер);
		Тело.Закрыть();
		
		СервисыИнтеграции.QLSDataExchange[ИмяКанала].ОтправитьСообщение(ИсходящееСообщение);
	Исключение
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ШаблонСообщения = НСтр("ru = 'Не удалось связаться с сервисом интеграции. По причине:
		|%1'");
		СообщениеОбОшибкеОтправкиВУзелОбмена = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, 
			ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке));
		
		ВызватьИсключение СообщениеОбОшибкеОтправкиВУзелОбмена;

	КонецПопытки;

	Возврат Истина;
	
КонецФункции

#КонецОбласти


#КонецЕсли
