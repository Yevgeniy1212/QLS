#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ПолучитьОписаниеОбработкиОбмена() Экспорт
	
	// полный состав параметров описания смотри в этой функции
	СтруктураНастроек = Справочники.НетиповыеОбменыДанными.ПолучитьОписаниеОбработкиОбменаПоУмолчанию();
	СтруктураНастроек.ТекстовоеОписаниеОбмена = "Выгрузка информации о статусах заказов в 1С Шину";
	СтруктураНастроек.ВидОбмена = Перечисления.ВидыОбмена.Выгрузка;
	СтруктураНастроек.СпособПередачиДанных = Перечисления.СпособыПередачиДанныхВНетиповыхОбменах.WebService_Host;
	СтруктураНастроек.XML_НастройкаОбязательна = Истина;	
	СтруктураНастроек.ДляМножественныхОбменов = Истина;
	СтруктураНастроек.ТипыПлановОбменовДляВыбора.Добавить(Тип("ПланОбменаСсылка.НетиповыеОбмены"));
	СтруктураНастроек.ИспользованиеРегламентогоЗадания = Истина;
	СтруктураНастроек.ИспользованиеРегистрацииДанныхНаУзлеНетиповогоОбмена = Истина;
	
	Возврат СтруктураНастроек;
	
КонецФункции 

Функция ВыполнитьОбмен(Событие, НастройкаОбмена) Экспорт	
	
	ТекстСообщения = "";
	
	НастройкаОбмена.ИмяФайлаВыгрузки = "BaseHttp_ВебИнтеграция_База1СВход";
	
	ОбработкаТранспортаОбменаДанными = Обработки.ТранспортСообщенийОбмена1СШина.Создать().Инициализировать(НастройкаОбмена);
	
	ДанныеВыгрузки = ПолучитьВыгружаемыеОбъектыОбмена(Событие, НастройкаОбмена);
	
	ПространствоИмен = ФорматОбмена();
	ТипОбъектаОбмена = ФабрикаXDTO.Тип(ПространствоИмен, "OrderStatus");
	
	СписокВыгруженыхОбъектов = Новый СписокЗначений;
	
	Для Каждого ОбъектКОтправке Из ДанныеВыгрузки.МассивОбъектов Цикл
		
		ОбъектОбмена = ФабрикаXDTO.Создать(ТипОбъектаОбмена);
		ОбъектОбмена.VIN = ОбъектКОтправке.VIN;
		ОбъектОбмена.status = Строка(ОбъектКОтправке.status);
			
		ОбъектОбмена.Проверить();
		
		СтрокаСообщение = JSON.Записать_JSON(ОбъектОбмена);
		
		ОбработкаТранспортаОбменаДанными.ОтправитьСообщение(СтрокаСообщение, ОбъектКОтправке.VIN);
		
		СписокВыгруженыхОбъектов.Добавить(ОбъектКОтправке.Ссылка);
		
	КонецЦикла;
	
	ВыгруженныеОбъекты = СтрСоединить(ДанныеВыгрузки.ДанныеНаУзле, ", ");
	
	ШаблонСообщения = НСтр("ru = 'Успешно отправлены ""%1"" в узел ""%2""'"); 
	ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения,
		ВыгруженныеОбъекты, 
		НастройкаОбмена.УзелРегистрацииИзменений);
	
	УровеньСобытия = Перечисления.СОЛЛЕРС_УровниСобытия.Информация;
	РегистрыСведений.СОЛЛЕРС_ЖурналРегистрацииСобытий.ЗафиксироватьИнформационноеСообщение(Событие, УровеньСобытия, ТекстСообщения);
	
	НетиповыеОбмены.ЗарегистрироватьВыгрузкуОбъектовИзПланаОбмена(НастройкаОбмена.Ссылка, СписокВыгруженыхОбъектов);		
	
	Возврат "";
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ФорматОбмена()
	
	Возврат "http://QLS/order-statuses"
	
КонецФункции

Функция ПолучитьВыгружаемыеОбъектыОбмена(Событие, НастройкаОбмена)
	
	МассивОбъектов = Новый Массив; 
	ДанныеНаУзле = Новый Массив;
	
	ДанныеВыгрузки = Новый Структура("МассивОбъектов, ДанныеНаУзле");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Узел", НастройкаОбмена.УзелРегистрацииИзменений);
	Запрос.УстановитьПараметр("НастройкаОбмена", НастройкаОбмена.Ссылка);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИзменениеСтатусаЗаказаИзменения.Ссылка.Заказ.VIN КАК VIN,
		|	ИзменениеСтатусаЗаказаИзменения.Ссылка.Статус КАК status,
		|	ИзменениеСтатусаЗаказаИзменения.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ИзменениеСтатусаЗаказа.Изменения КАК ИзменениеСтатусаЗаказаИзменения
		|ГДЕ
		|	ИзменениеСтатусаЗаказаИзменения.Узел = &Узел";
	
	Запрос.УстановитьПараметр("Узел", НастройкаОбмена.УзелРегистрацииИзменений);			
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл	
		СтруктураДанных = Новый Структура("VIN, status, Ссылка");
		ЗаполнитьЗначенияСвойств(СтруктураДанных, Выборка);
		МассивОбъектов.Добавить(СтруктураДанных);	
		ДанныеНаУзле.Добавить(СтруктураДанных.VIN);
	КонецЦикла;
	
	ДанныеВыгрузки.МассивОбъектов = МассивОбъектов;
	ДанныеВыгрузки.ДанныеНаУзле = ДанныеНаУзле;
	
	Возврат ДанныеВыгрузки;
	
КонецФункции   

#КонецОбласти

#КонецЕсли
